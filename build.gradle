plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'

    alias(libs.plugins.shadow)

    id 'tech.yanand.maven-central-publish' version '1.2.0'
}

group = 'ru.swat1x'
version = '2.1'

java {
    targetCompatibility = sourceCompatibility = JavaVersion.VERSION_1_8

    withSourcesJar()
    withJavadocJar()
}


repositories {
    mavenCentral()
}

dependencies {
    compileOnlyApi libs.lombok
    annotationProcessor libs.lombok

    compileOnlyApi libs.jetbrains.annotations
    compileOnlyApi libs.logger.slf4j.api
    compileOnlyApi libs.logger.log4j.api

    api libs.database.sql.hikaricp
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                groupId = project.group
                artifactId = project.name
                version = project.version

                description = "Library for connection with SQL databases"
                url = "https://github.com/swat1x/database-sql"

                developers {
                    developer {
                        name = "Vadim Smyshlyaev"
                        email = "swat1x@internet.ru"
                    }
                }

                licenses {
                    license {
                        name = "The Apache Software License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/swat1x/database-sql.git"
                    developerConnection = "scm:git:ssh://github.com:swat1x/database-sql.git"
                    url = "http://github.com/swat1x/database-sql/tree/master"
                }

            }

        }
    }
    repositories {
        maven {
            name = "Local"
            url = layout.buildDirectory.dir('repos/bundles')
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

mavenCentral {
    repoDir = layout.buildDirectory.dir('repos/bundles')
    authToken = findProperty("ossrhToken")
    publishingType = 'AUTOMATIC'
    maxWait = 60
}
